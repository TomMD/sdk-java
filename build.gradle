//
// build.gradle
//
// Copyright (C) 2016, Tozny, LLC.
// All Rights Reserved.
//

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'jacoco'

group = 'com.github.tozny'
description = 'tozny-sdk'

repositories {
  mavenCentral()
}

dependencies {
  compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations',  version: '[2.4,3.0['
  compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind',     version: '[2.4,3.0['
  compile group: 'com.squareup.okhttp3',       name: 'okhttp',               version: '3.+'
  compile group: 'com.google.code.findbugs',   name: 'jsr305',               version: '+'
  compile group: 'commons-codec',              name: 'commons-codec',        version: '1.+'

  testCompile group: 'junit', name: 'junit', version: '4.12'

  testRuntime files('test.properties')
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

install {
  repositories.mavenInstaller {
    pom.project {
      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0'
          distribution 'repo'
        }
      }
    }
  }
}

test {
  systemProperties = [
    testProperties: 'test.properties'
  ]
  testLogging {
    showStandardStreams = true
  }
}

jacocoTestReport {
  reports {
    xml.enabled = true
    html.enabled = true
  }
}

check.dependsOn jacocoTestReport

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

// vim: set ft=groovy ts=2 et:
