//
// build.gradle
//
// Copyright (C) 2016, Tozny, LLC.
// All Rights Reserved.
//

allprojects {
  apply plugin: 'java'

  repositories {
    maven { url "http://repo.maven.apache.org/maven2" }
  }
}

apply plugin: 'jacoco'

group = 'com.tozny.sdk'

def baseVersion = "2.0"
def buildNumber = System.getenv("BUILD_NUMBER")

if (buildNumber != null) {
  version = "$baseVersion.$buildNumber"
} else {
  version = "$baseVersion-SNAPSHOT"
}

description = 'tozny-sdk'

dependencies {
  compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations',  version: '2.+'
  compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind',     version: '2.+'
  compile group: 'com.squareup.okhttp3',       name: 'okhttp',               version: '3.+'
  compile group: 'com.google.code.findbugs',   name: 'jsr305',               version: '+'

  testCompile group: 'junit', name: 'junit', version: '4.12'

  testRuntime files('test.properties')
}

test {
  systemProperties = [
    testProperties: 'test.properties'
  ]
}

jacocoTestReport {
  reports {
    xml.enabled = true
    html.enabled = true
  }
}

check.dependsOn jacocoTestReport

// vim: set ft=groovy ts=2 et:
